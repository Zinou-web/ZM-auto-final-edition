plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
}

android {
    namespace 'com.example.myapplication'
    compileSdk 34

    defaultConfig {
        applicationId "com.example.myapplication"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        // Enable Java 8+ API desugaring
        multiDexEnabled true

        // Enable view binding if not already enabled
        buildFeatures {
            viewBinding true
            compose true
            buildConfig true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "API_BASE_URL", "\"https://api.zmrentals.com/\""
        }
        debug {
            buildConfigField "String", "API_BASE_URL", "\"https://ec86-105-111-108-46.ngrok-free.app/\""
        }
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.4'
    }
    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            excludes += 'META-INF/DEPENDENCIES'
            excludes += 'META-INF/LICENSE'
            excludes += 'META-INF/LICENSE.txt'
            excludes += 'META-INF/license.txt'
            excludes += 'META-INF/NOTICE'
            excludes += 'META-INF/NOTICE.txt'
            excludes += 'META-INF/notice.txt'
            excludes += 'META-INF/ASL2.0'
            excludes += 'META-INF/*.kotlin_module'
        }
    }

    // Add lint configuration to use a baseline
    lint {
        abortOnError = false
        checkReleaseBuilds = false
        baseline = file("lint-baseline.xml")
    }
}


dependencies {
    // Update WorkManager to latest version that handles PendingIntent flags properly
    implementation "androidx.work:work-runtime-ktx:2.8.1"
    implementation libs.androidx.hilt.work

    def composeBom = platform('androidx.compose:compose-bom:2024.02.00')
    implementation composeBom
    androidTestImplementation composeBom

    // Facebook SDK
    implementation 'com.facebook.android:facebook-login:16.3.0'
    implementation 'com.facebook.android:facebook-android-sdk:16.3.0'

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation "androidx.navigation:navigation-compose:2.7.7"

    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation "androidx.compose.material:material-icons-extended"
    implementation "androidx.compose.material:material"

    // Add Google Material Design library for the View-based Snackbar
    implementation 'com.google.android.material:material:1.11.0'

    implementation "io.coil-kt:coil-compose:2.5.0"

    // Network
    implementation ('com.squareup.retrofit2:retrofit:2.9.0') {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation ('com.squareup.retrofit2:converter-gson:2.9.0') {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp' // Also exclude from converter just in case
    }
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:4.12.0'
    implementation 'com.google.code.gson:gson:2.10.1'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'

    // Dependency Injection
    implementation "com.google.dagger:hilt-android:2.51.1"
    ksp "com.google.dagger:hilt-android-compiler:2.51.1"
    implementation 'androidx.hilt:hilt-navigation-compose:1.1.0'

    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.7.0'

    // Image Handling
    implementation ('com.github.dhaval2404:imagepicker:2.1') {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Google Sign-In
    implementation 'com.google.android.gms:play-services-auth:20.7.0'

    // Security
    implementation "androidx.security:security-crypto:1.1.0-alpha06"
    implementation "androidx.security:security-crypto-ktx:1.1.0-alpha06"

    // DataStore for preferences
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
    testImplementation 'org.mockito:mockito-core:5.5.0'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:5.1.0'
    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'app.cash.turbine:turbine:1.0.0'

    // AndroidX Test
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    // Hilt Testing
    testImplementation "com.google.dagger:hilt-android-testing:2.51.1"
    kspTest "com.google.dagger:hilt-android-compiler:2.51.1"
    androidTestImplementation "com.google.dagger:hilt-android-testing:2.51.1"
    kspAndroidTest "com.google.dagger:hilt-android-compiler:2.51.1"

    debugImplementation 'androidx.compose.ui:ui-tooling'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
}
